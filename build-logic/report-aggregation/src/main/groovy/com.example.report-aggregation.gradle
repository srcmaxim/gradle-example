plugins {
    id('java-base') // we only apply 'java-base' as this plugin is for projects without source code
    id('jacoco')
    id('checkstyle')
}

// Configurations to declare dependencies
def aggregate = configurations.create('aggregate') {
    visible = false
    canBeResolved = false
    canBeConsumed = false
}

// Resolvable configuration to resolve the classes of all dependencies
def classPath = configurations.create('classPath') {
    visible = false
    canBeResolved = true
    canBeConsumed = false
    extendsFrom(aggregate)
    attributes {
        attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
        attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
        attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.CLASSES))
        attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
    }
}

// A resolvable configuration to collect source code
def sourcesPath = configurations.create('sourcesPath') {
    visible = false
    canBeResolved = true
    canBeConsumed = false
    extendsFrom(aggregate)
    attributes {
        attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
        attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
        attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'source-folders'))
    }
}

// A resolvable configuration to collect JaCoCo coverage data
def coverageDataPath = configurations.create('coverageDataPath') {
    visible = false
    canBeResolved = true
    canBeConsumed = false
    extendsFrom(aggregate)
    attributes {
        attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
        attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
        attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, 'jacoco-coverage-data'))
    }
}

// Register a code coverage report task to generate the aggregated report
def codeCoverageReport = tasks.register('codeCoverageReport', JacocoReport) {
    additionalClassDirs(classPath.filter { it.isDirectory() })
    additionalSourceDirs(sourcesPath.incoming.artifactView { lenient(true) }.files)
    executionData(coverageDataPath.incoming.artifactView { lenient(true) }.files.filter { it.exists() })

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

// Register a linter report task to generate the aggregated report
def linterReport = tasks.register('linterReport', Checkstyle) {
    setSource(sourcesPath.incoming.artifactView { lenient(true) }.files)
    setClasspath(classPath.filter { it.isDirectory() })

    config project.resources.text.fromFile(getCheckstyleFile())

    // Exclude :domain:avro-event from reports
    exclude '**/com/example/domain/event/**'

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

private File getCheckstyleFile() {
    def projectDir = rootProject.projectDir.toPath()
    if (!projectDir.endsWith('aggregation')) {
        throw new IllegalStateException('Root Project dir is not /aggregation')
    }
    return projectDir
            .resolveSibling('build-logic/commons')
            .resolve('src/main/resources/checkstyle-java-google-style.xml')
            .toFile()
}

// Make JaCoCo, CheckStyle report generation part of the 'check' lifecycle phase
tasks.named('check') {
    dependsOn(codeCoverageReport)
    dependsOn(linterReport)
}
